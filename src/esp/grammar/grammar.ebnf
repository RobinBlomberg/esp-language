AdditiveExpression ::=
  | MultiplicativeExpression
  | AdditiveExpression '+' MultiplicativeExpression
  | AdditiveExpression '-' MultiplicativeExpression

ArgumentList ::=
  | Expression
  | ArgumentList ',' Expression

Arguments ::=
  | '(' ArgumentList? ')'

ArrayLiteral ::=
  | '[' ExpressionList ']'
  // | '[' ElementList ']' (replaces line above)

AssignmentOperator ::=
  | '='
  | '*='
  | '/='
  | '%='
  | '+='
  | '-='
  | '<<='
  | '>>='
  | '>>>='
  | '&='
  | '^='
  | '|='
  | '**='
  | '&&='
  | '||='

BitwiseANDExpression ::=
  | EqualityExpression
  | BitwiseANDExpression '&' EqualityExpression

BitwiseORExpression ::=
  | BitwiseXORExpression
  | BitwiseORExpression '|' BitwiseXORExpression

BitwiseXORExpression ::=
  | BitwiseANDExpression
  | BitwiseXORExpression '^' BitwiseANDExpression

BlockStatement ::=
  | '{' StatementList? '}'

BooleanLiteral ::=
  | 'true'
  | 'false'

BreakStatement ::=
  | 'break' ';'

CaseBlock ::=
  | '{' CaseClauses '}'
  | '{' CaseClauses 'else' Statement '}'

CaseClause ::=
  | ExpressionList Statement

CaseClauses ::=
  | CaseClause
  | CaseClauses CaseClause

ConditionalExpression ::=
  | LogicalORExpression
  | LogicalORExpression '?' Expression ':' Expression

ConstantKeyword ::=
  | 'false'
  | 'Infinity'
  | 'NaN'
  | 'null'
  | 'true'
  | 'undefined'

ContinueStatement ::=
  | 'continue' ';'

ControlKeyword ::=
  | 'break'
  | 'const'
  | 'continue'
  | 'do'
  | 'else'
  | 'for'
  | 'if'
  | 'let'
  | 'match'
  | 'new'
  | 'return'
  | 'throw'
  | 'while'

CoverParenthesizedExpression ::=
  | '(' Expression ')'
  // | '(' Expression ')' IdentifierName

DecimalDigit ::=
  | [0-9]

DecimalDigits ::=
  | DecimalDigit
  | DecimalDigits DecimalDigit

DecimalIntegerLiteral ::=
  | '0'
  | NonZeroDigit DecimalDigits?

DoubleStringCharacter ::=
  | [^"\]
  | '\\' [.]

DoubleStringCharacters ::=
  | DoubleStringCharacter DoubleStringCharacters?

DoWhileStatement ::=
  | 'do' Statement 'while' '(' Expression ')' ';'

/*
ElementList ::=
  | Expression
  | SpreadElement
  | ElementList ',' Expression
  | ElementList ',' SpreadElement
*/

EqualityExpression ::=
  | RelationalExpression
  | EqualityExpression '==' RelationalExpression
  | EqualityExpression '!=' RelationalExpression

ExponentiationExpression ::=
  | UnaryExpression
  // | RangeExpression (replaces line above)
  | UpdateExpression '**' ExponentiationExpression
  // | RangeExpression '**' ExponentiationExpression (replaces line above)

Expression ::=
  | ConditionalExpression
  // | IfExpression
  | Function
  | LeftHandSideExpression AssignmentOperator Expression

ExpressionList ::=
  | Expression
  | ExpressionList ',' Expression

ExpressionStatement ::=
  | Expression ';'

ForStatement ::=
  | 'for' '(' VariableDeclaration? ';' Expression? ';' Expression? ')' Statement
  | 'for' '(' LetOrConst Identifier 'of' Expression ')' Statement

Function ::=
  | ':' '(' ParameterList? ')' BlockStatement
  | ':' '(' ParameterList? ')' Expression

Identifier ::=
  | IdentifierName - Keyword
  // | '*' IdentifierName

IdentifierName ::=
  | IdentifierStartChar
  | IdentifierName IdentifierPartChar

IdentifierPartChar ::=
  | [a-zA-Z0-9$_]

IdentifierStartChar ::=
  | [a-zA-Z$_]

/*
IfExpression ::=
  | 'if' '(' Expression ')' Expression
  | 'if' '(' Expression ')' Expression 'else' Expression
*/

IfStatement ::=
  | 'if' '(' Expression ')' Statement
  | 'if' '(' Expression ')' Statement 'else' Statement

/*
InlineDeclaration ::=
  | InlineIdentifier '=' Literal ';'

InlineIdentifier ::=
  | '#' IdentifierName
*/

Keyword ::=
  | ConstantKeyword
  | ControlKeyword

LeftHandSideExpression ::=
  | PrimaryExpression
  | LeftHandSideExpression '[' Expression ']'
  | LeftHandSideExpression '.' IdentifierName
  | LeftHandSideExpression Arguments
  | 'new' LeftHandSideExpression Arguments

LetOrConst ::=
  | 'let'
  | 'const'

Literal ::=
  | BooleanLiteral
  | NumericLiteral
  | StringLiteral
  | 'null'
  | 'undefined'

LogicalANDExpression ::=
  | BitwiseORExpression
  | LogicalANDExpression '&&' BitwiseORExpression

LogicalORExpression ::=
  | LogicalANDExpression
  | LogicalORExpression '||' LogicalANDExpression

MatchStatement ::=
  | 'match' '(' Expression ')' CaseBlock

MultiplicativeExpression ::=
  | ExponentiationExpression
  | MultiplicativeExpression MultiplicativeOperator ExponentiationExpression

MultiplicativeOperator ::=
  | '*'
  | '/'
  | '%'

NonZeroDigit ::=
  | [1-9]

NumericLiteral ::=
  | DecimalIntegerLiteral
  | DecimalIntegerLiteral '.' DecimalDigits
  // | DecimalIntegerLiteral IdentifierName
  // | DecimalIntegerLiteral '.' IdentifierName

ObjectLiteral ::=
// MapLiteral ::= (replaces line above)
  | '{' '}'
  | '{' PropertyDefinitionList '}'

/*
ObjectLiteral ::=
  | '{{' '}}'
  | '{{' PropertyDefinitionList '}}'
*/

Parameter ::=
  | Identifier

ParameterList ::=
  | Parameter
  | ParameterList ',' Parameter

PropertyDefinition ::=
  | PropertyName ':' Expression

PropertyDefinitionList ::=
  | PropertyDefinition
  | PropertyDefinitionList ',' PropertyDefinition

PropertyName ::=
  | IdentifierName

PrimaryExpression ::=
  | Identifier
  | Literal
  | ArrayLiteral
  | ObjectLiteral
  | SetLiteral
  | CoverParenthesizedExpression

/*
RangeExpression ::=
  | Expression '..' Expression
  | Expression '..=' Expression
  | Expression '..' Expression '..' Expression
  | Expression '..' Expression '..=' Expression
*/

RelationalExpression ::=
  | ShiftExpression
  | RelationalExpression '<' ShiftExpression
  | RelationalExpression '>' ShiftExpression
  | RelationalExpression '<=' ShiftExpression
  | RelationalExpression '>=' ShiftExpression
  // | RelationalExpression 'in' ShiftExpression
  // | RelationalExpression 'in' UnionExpression

ReturnStatement ::=
  | 'return' Expression ';'

SetLiteral ::=
  | '{' '[' ExpressionList ']' '}'

ShiftExpression ::=
  | AdditiveExpression
  | ShiftExpression '<<' AdditiveExpression
  | ShiftExpression '>>' AdditiveExpression
  | ShiftExpression '>>>' AdditiveExpression

SingleStringCharacter ::=
  | [^'\]
  | '\\' [.]

SingleStringCharacters ::=
  | SingleStringCharacter SingleStringCharacters?

/*
SpreadElement ::=
  | '...' Expression
  | 'void'
  | IfExpression
  | PrimaryExpression '?'
*/

Statement ::=
  | BlockStatement
  | BreakStatement
  | ContinueStatement
  | DoWhileStatement
  | ExpressionStatement
  | ForStatement
  | IfStatement
  | MatchStatement
  | ReturnStatement
  | ThrowStatement
  | VariableDeclaration
  | WhileStatement

StatementList ::=
  | Statement
  | StatementList Statement

StringLiteral ::=
  | '"' DoubleStringCharacters? '"'
  | "'" SingleStringCharacters? "'"

ThrowStatement ::=
  | 'throw' Expression ';'

UnaryExpression ::=
  | UpdateExpression
  | '-' UnaryExpression
  | '!' UnaryExpression

/*
UnionExpression ::=
  | '(' ExpressionList ')'
*/

UpdateExpression ::=
  | LeftHandSideExpression
  | LeftHandSideExpression '++'
  | LeftHandSideExpression '--'
  | '++' UnaryExpression
  | '--' UnaryExpression

VariableDeclaration ::=
  | LetOrConst Identifier '=' Expression ';'

WhileStatement ::=
  | 'while' '(' Expression ')' Statement
  // | 'while' Statement
